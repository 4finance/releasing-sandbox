//Based on release.gradle from Mockito project by Szczepan Faber - https://github.com/mockito/mockito
assert project == rootProject

//To allow to use classes from buildscript dependencies that secion has to be in every included file
buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '0.9.9'
        classpath "org._10ne.gradle:rest-gradle-plugin:0.3.1"
    }
}

apply from: rootProject.file("gradle/publish.gradle")

def dryRun = project.hasProperty('dryRun')
def branch = System.env.TRAVIS_BRANCH
boolean localRelease = project.hasProperty("localRelease")
boolean isTriggerInCommit = isReleaseTriggerInLastCommitMessage(localRelease)

task("releaseNeeded") {
    println "Checking if release is needed..."
    doLast {
        def pr = System.env.TRAVIS_PULL_REQUEST
        def ci = System.env.CI
//        ext.needed = dryRun || (pr == 'false' && branch == 'master' && isTriggerInCommit) || (ci == null && pr == null && localRelease)
        ext.needed = dryRun || (pr == 'false' /*&& branch == 'master'*/  && isTriggerInCommit) || (ci == null && pr == null && localRelease)  //TODO: master is ok in sandbox project
        logger.lifecycle("Release needed: {}, branch: {}, pull request: {}, dry run: {}, ci: {}, isTriggerInCommit: {}, localRelease: {}.",
                needed, branch, pr, dryRun, ci, isTriggerInCommit, localRelease)

        if (dryRun) {
            //TODO: Does not impact on Axion - earlier evaluated?
            project.ext.set("release.dryRun", "")
        }
    }
}

bintrayUpload {
    dependsOn releaseNeeded
    onlyIf { releaseNeeded.needed }
    doFirst {
        if (dryRun) {
            logger.lifecycle "Dry-running bintrayUpload"
        }
    }
}

release {
    dependsOn releaseNeeded
    onlyIf { releaseNeeded.needed }
    doFirst {
        if (dryRun) {
            println "release.dryRun: " + project.hasProperty("release.dryRun")
            logger.lifecycle "Dry-running Axion release"
        }
    }
}

task("finalizeRelease") {
    dependsOn bintrayUpload, releaseNeeded
    onlyIf { releaseNeeded.needed }
    doLast {
        if (dryRun) {
            logger.lifecycle "Dry-running finalizeRelease"
        }
        println "Finalizing release..."

        def grgit = Grgit.open(getRootDir())
        def pushParams = [tags: true, remote: "origin"]
        if (!localRelease) {
            pushParams << [refsOrSpecs: ["HEAD:${branch}"]]
        }
//    //TODO: Not supported in Grgit yet - my PR is waiting for merge - https://github.com/ajoberstar/grgit/pull/44
//    if (ext.dryRun) {
//        pushParams.put(dryRun: true)
//    }
//    grgit.push(pushParams)

        if (dryRun) {
            println "Dry-running push to GH with tags"
        } else {
            grgit.push(pushParams)
        }
    }
}

//TODO: Move earlier than making commit by Axion
private void configureGit() {
    runCommand "git", "config", "user.email", "4financebot@gmail.com"
    runCommand "git", "config", "user.name", "4Finance Bot"
    runCommand "git", "config", "push.default", "simple"
    runCommand "git", "remote", "set-url", "origin", new MaskedArg(value: "https://${System.env.GH_TOKEN}@github.com/4finance/releasing-sandbox.git")
}

private void runCommand(Object ... args) {
    runCommand(args as List)
}
private void runCommand(Collection args) {
    logger.lifecycle("--- Executing: {}", args.join(" "))
    exec { commandLine args.collect { it instanceof MaskedArg? it.value : it.toString()} }
    logger.lifecycle("--- Completed!", args)
}
class MaskedArg {
    String value
    String toString() { "<masked>" }
}

//TODO: Due to new Gradle import format it cannot be moved to release.gradle
import org.ajoberstar.grgit.Commit
import org.ajoberstar.grgit.Grgit

private boolean isReleaseTriggerInLastCommitMessage(boolean isLocalRelease) {
    final String RELEASE_TRIGGER = '#DO_RELEASE'

    def grgit = Grgit.open(getRootDir())
    List<Commit> history = []

    if (isLocalRelease) {
        history = grgit.log(maxCommits: 1)
    } else {
        def travisBranch = System.env.TRAVIS_BRANCH
        if (travisBranch != null) {
            history = grgit.log(includes: [travisBranch], maxCommits: 1)
        }
    }

    println history
    return history[0]?.fullMessage?.contains(RELEASE_TRIGGER)
}

task publishUploadedArtifacts(type: org._10ne.gradle.rest.RestTask) {
    dependsOn releaseNeeded
    mustRunAfter finalizeRelease
    onlyIf { releaseNeeded.needed && !project.hasProperty('dryRun') }  //TODO: Not available in rest-gradle-plugin yet - https://github.com/noamt/rest-gradle-plugin/issues/9
    httpMethod = 'post'
    uri = "https://api.bintray.com/content/4finance/${bintray.pkg.repo}/${project.name}/${project.version}/publish"
    username = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : System.properties['bintrayUser']
    password = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : System.properties['bintrayKey']
    contentType = groovyx.net.http.ContentType.JSON
    requestBody = [discard: 'true'] //TODO: For testing Travis - change to false
}
